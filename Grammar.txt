<START> ::= <STATEMENTS>

<STATEMENTS> ::= <STATEMENTS> <STATEMENT>
               | <STATEMENT>

<STATEMENT> ::= <IMPRESION>
              | <DECLARATION>
              | <ASSIGNATION>
              | <IF_STMT>
              | <INCREMENT>
              | <DECREMENT>
              | <WHILE_STMT>
              | <BREAK>
              | <CONTINUE>
              | "error" <ENDLINE>

<BREAK> ::= "break" <ENDLINE>

<CONTINUE> ::= "continue" <ENDLINE>

<IMPRESION> ::= "println" "(" <EXPRESION> ")" <ENDLINE>

<DECLARATION> ::= "const" <ID> ":" <TYPES> "=" <EXPRESION> <ENDLINE>
                | "const" <ID> ":" <TYPES> <ENDLINE>
                | "var" <ID> ":" <TYPES> <ENDLINE>
                | "var" <ID> ":" <TYPES> "=" <EXPRESION> <ENDLINE>

<ASSIGNATION> ::= <ID> "=" <EXPRESION> <ENDLINE>

<IF_STMT> ::= "if" "(" <EXPRESION> ")" "{" <STATEMENTS> "}"
            | "if" "(" <EXPRESION> ")" "{" <STATEMENTS> "}" "else" "{" <STATEMENTS> "}"

<WHILE_STMT> ::= "while" "(" <EXPRESION> ")" "{" <STATEMENTS> "}"

<INCREMENT> ::= <ID> "++" <ENDLINE>

<DECREMENT> ::= <ID> "--" <ENDLINE>

<TYPES> ::= "int"
          | "double"
          | "string"
          | "bool"
          | "char"

<EXPRESION> ::= "-" <EXPRESION>
              | <EXPRESION> "*" <EXPRESION>
              | <EXPRESION> "/" <EXPRESION>
              | <EXPRESION> "**" <EXPRESION>
              | <EXPRESION> "%" <EXPRESION>
              | <EXPRESION> "-" <EXPRESION>
              | <EXPRESION> "+" <EXPRESION>
              | <EXPRESION> "==" <EXPRESION>
              | <EXPRESION> "!=" <EXPRESION>
              | <EXPRESION> "<" <EXPRESION>
              | <EXPRESION> "<=" <EXPRESION>
              | <EXPRESION> ">" <EXPRESION>
              | <EXPRESION> ">=" <EXPRESION>
              | "!" <EXPRESION>
              | <EXPRESION> "||" <EXPRESION>
              | <EXPRESION> "&&" <EXPRESION>
              | <EXPRESION> "^" <EXPRESION>
              | <INTEGER>
              | <DECIMAL>
              | <STRING>
              | <CHAR>
              | "true"
              | "false"
              | <ID>
              | "(" <EXPRESION> ")"
